plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

apply from: rootProject.file("gradle/git-tag-version.gradle")

android {
    namespace 'no.nordicsemi.android.blinky'
    compileSdk android_api_version

    defaultConfig {
        applicationId "no.nordicsemi.android.nrfblinky"
        minSdk android_min_api_version
        targetSdk android_api_version
        versionCode getVersionCodeFromTags()
        versionName getVersionNameFromTags()
        resConfigs "en"
    }

    signingConfigs {
        release {
            storeFile file('../keystore')
            storePassword System.env.KEYSTORE_PSWD
            keyAlias System.env.KEYSTORE_ALIAS
            keyPassword System.env.KEYSTORE_KEY_PSWD
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    /**
     * There is an issue with recomposition performance when data objects doesn't belong to the module
     * when composition is enabled, because those objects cannot be properly compared for the changes.
     * Better to leave enabled for all modules.
     */
    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }

    buildFeatures {
        compose true
    }
}
hilt {
    enableAggregatingTask = true
}
kapt {
    correctErrorTypes true
}

dependencies {
    implementation project(':scanner')
    implementation project(':blinky:spec')
    implementation project(':blinky:ui')
    implementation project(':blinky:ble')

    implementation "no.nordicsemi.android.common:theme:$nordic_common_version"
    implementation "no.nordicsemi.android.common:navigation:$nordic_common_version"

    // BLE events are logged using Timber.
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Fundamental components of compose UI
    implementation "androidx.compose.ui:ui:$compose_version"
    // Previews
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation "androidx.compose.foundation:foundation:$compose_version"
    // Material 3
    implementation "androidx.compose.material3:material3:$material3_version"
    // Integration with activities
    implementation "androidx.activity:activity-compose:1.6.0"

    // Hilt and Dagger
    implementation "com.google.dagger:hilt-android:$dagger_version"
    kapt "com.google.dagger:hilt-compiler:$dagger_version"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
}